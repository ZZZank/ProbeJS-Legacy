// ModTemplatez: An Architectury Loom based template workspace for Minecraft Forge mod development.
//
// ideally you don't need to touch this gradle script, instead:
//
// see ./gradle/scripts/dependencies.gradle for dependency management
//
// see ./gradle/scripts/publishing.gradle for publishing
//
// see ./gradle/scripts/extra.gradle for adding custom action after publishing
//
// see ./gradle.properties for almost everything about mod metadata, mod compiling, Minecraft sources, etc.
//
//
//
//
//

import org.jetbrains.changelog.Changelog;

plugins {
    id('dev.architectury.loom').version('1.10-SNAPSHOT')
    id('maven-publish')
    id('idea')
    id('com.matthewprenger.cursegradle').version('1.4.0').apply(false) //only used for CF publishing
    id('com.modrinth.minotaur').version('2.+').apply(false)            //only used for Modrinth publishing
    id('org.jetbrains.changelog').version('2.2.0')
    id('xyz.wagyourtail.jvmdowngrader').version("${jvmdowngrader_version}").apply(false)
}

apply from: '../gradle/scripts/helpers.gradle'

String TARGET_JAVA = "8"
boolean USE_JVMDG = true
String RESOURCE_PACK_FORMAT = "6"
String FORGE_VERSION = "36.2.39"
String LOADER_VERSION_RANGE = "[36,)"
String MC_VERSION = "1.16.5"
String MC_RANGE = "[$MC_VERSION,${nextMCVersion(MC_VERSION)})"

apply from: '../gradle/scripts/custom_tasks.gradle'

def javaTarget = JavaVersion.toVersion(TARGET_JAVA)
println("Target Java version is set to: ${javaTarget.majorVersion}")

if (USE_JVMDG) {
    apply plugin: 'xyz.wagyourtail.jvmdowngrader'
    jvmdg.downgradeTo = javaTarget
}

base {
    archivesName = "${propertyString('archives_base_name')}-${MC_VERSION}"
}

version = propertyString('mod_version')
group = propertyString('maven_group')

java {
    if (!USE_JVMDG) {
        sourceCompatibility = targetCompatibility = javaTarget
    }

    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    if (propertyBool('generate_sources_jar')) {
        withSourcesJar()
    }
    if (propertyBool('generate_javadocs_jar')) {
        withJavadocJar()
    }
}

loom {
    // use this if you are using the official mojang mappings
    // and want loom to stop warning you about their license
    silentMojangMappingsLicense()

    if (propertyBool('use_access_widener')) {
        def f = file("src/main/resources/${propertyString('access_widener_name')}.accesswidener")
        if (!f.exists()) {
            logger.warn("Configured AccessWidener not found, force generating")
            fnGenerateAccessWidenerBase()
        }
        accessWidenerPath = f
    }

    // since loom 0.10, you are **required** to use the
    // "forge" block to configure forge-specific features,
    // such as the mixinConfigs array or datagen
    forge {
        // specify the mixin configs used in this mod
        // this will be added to the jar manifest as well!
        if (propertyBool('use_mixin')) {
            it.mixinConfigs.set(propertyStringList('mixin_configs').collect {"${it}.json"})
        }

        if (propertyBool('use_access_widener')) {
            convertAccessWideners = true
        }
    }
}

repositories {
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

configurations.configureEach {
    resolutionStrategy.eachDependency {
        if (requested.group == "org.ow2.asm") {
            useVersion("9.6")
            because("Force ASM to a modern version that supports Java 21")
        }
        if (requested.group == "org.lwjgl") {
            useVersion("3.3.3")
            because("Force LWJGL to a modern version that supports Java 21")
        }
    }
}

dependencies {
    shade(project(":srcCore"))

    minecraft("com.mojang:minecraft:${MC_VERSION}")

    mappings(loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.16.5:2022.03.06")
    })

    forge("net.minecraftforge:forge:${MC_VERSION}-${FORGE_VERSION}")
}

apply(from: 'dependencies.gradle')

processResources {
    if (!propertyBool("use_access_widener")) {
        exclude("${propertyString('access_widener_name')}.accesswidener")
    }

    if (!propertyBool('use_mixin')) {
        exclude(propertyStringList('mixin_configs').collect({"${it}.json"}))
    }

    def filters = ['META-INF/mods.toml', 'pack.mcmeta']

    filesMatching(filters) {
        it.expand(
            "version": propertyString('mod_version'),
            "forge_version": FORGE_VERSION,
            "loader_version_range": LOADER_VERSION_RANGE,
            "license": propertyString('license'),
            "mod_id": propertyString('mod_id'),
            "mod_display_name": propertyString('mod_display_name'),
            "mod_author": propertyString('mod_author'),
            "mod_description": propertyString('mod_description'),
            "minecraft_version": MC_VERSION,
            "required_minecraft_range": MC_RANGE,
            "resource_pack_format": RESOURCE_PACK_FORMAT
        )
    }
}

remapJar {
    if (USE_JVMDG) {
        setInput(shadeDowngradedApi.archiveFile)
    } else {
        setInput(shadowJar.archiveFile)
    }
    dependsOn(shadowJar)
}

if (USE_JVMDG) {
    assemble.dependsOn(shadeDowngradedApi)
} else {
    assemble.dependsOn(shadowJar)
}

String parserChangelog() {
    if (!file('CHANGELOG.md').exists()) {
        throw new GradleException('publish_with_changelog is true, but CHANGELOG.md does not exist in the workspace!')
    }
    String parsedChangelog = changelog.renderItem(
        changelog.get(propertyString('mod_version')).withHeader(false).withEmptySections(false),
        Changelog.OutputType.MARKDOWN
    )
    if (parsedChangelog.isEmpty()) {
        throw new GradleException('publish_with_changelog is true, but the changelog for the latest version is empty!')
    }
    return parsedChangelog
}

processResources.dependsOn(generateMixinJson, generateAccessWidenerBase)

apply(from: 'publishing.gradle')
apply(from: 'extra.gradle')
